plugins {
    id 'multiloader-base'
    id 'net.neoforged.moddev'
    id 'xyz.jpenilla.resource-factory-neoforge-convention'
    id 'io.github.dexman545.outlet' apply false
    id 'com.modrinth.minotaur' apply false
}

neoForge {
    runs {

    }
}

allprojects {
    apply plugin: "multiloader-base"
    apply plugin: "net.neoforged.moddev"
    apply plugin: "xyz.jpenilla.resource-factory-neoforge-convention"

    neoForgeModsToml {
        issueTrackerUrl = rootProject.mod_issues
        mod(rootProject.mod_id) {
            version = project.version
            displayName = rootProject.mod_name
            description = rootProject.mod_description
            logoFile = "assets/${rootProject.mod_id}/icon.png"
            logoBlur = false
            features = ["javaVersion": "[21,)"]
            modUrl = rootProject.mod_sources
            displayUrl = rootProject.mod_website
            dependencies {
                if (project.hasProperty("minecraft_version_range")) required("minecraft",  project.findProperty("minecraft_version_range"))
            }
        }
    }

    dependencies {
        compileOnly project(":common")
        commonJava project(path: ":common", configuration: "commonJava")
        commonResources project(path: ":common", configuration: "commonResources")
        compileOnly project(":modded")
        commonJava project(path: ":modded", configuration: "commonJava")
        commonResources project(path: ":modded", configuration: "commonResources")
    }

    neoForge {
        version = project.neoforge_version

        parchment {
            minecraftVersion = project.parchment_minecraft_version
            mappingsVersion = project.parchment_mappings_version
        }

        mods {
            "${project.name}" {
                sourceSet(sourceSets.main)
            }
        }
    }
}

subprojects {
    apply plugin: "net.neoforged.moddev"
    apply plugin: "io.github.dexman545.outlet"
    apply plugin: "com.modrinth.minotaur"

    tasks.modrinth.dependsOn(tasks.modrinthSyncBody)

    outlet {
        mcVersionRange = project.supported_minecraft_versions
    }

    configurations {
        runtimeClasspath.extendsFrom localRuntime

        includeRuntime
        named(neoForge.versionCapabilities.legacyClasspath() ? "additionalRuntimeClasspath" : "runtimeOnly").get().extendsFrom includeRuntime
        jarJar.extendsFrom includeRuntime
    }

    repositories {
        maven {
            name = "DevAuth"
            url = "https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1"
            content {
                includeGroup "me.djtheredstoner"
            }
        }
    }

    dependencies {
        compileOnly project(":loader:neoforge")
        commonJava project(path: ":loader:neoforge", configuration: "commonJava")
        commonResources project(path: ":loader:neoforge", configuration: "commonResources")

        compileOnly project(":modded:${project.commonModdedVersion}")
        commonJava project(path: ":modded:${project.commonModdedVersion}", configuration: "commonJava")
        commonResources project(path: ":modded:${project.commonModdedVersion}", configuration: "commonResources")

        // DevAuth
        localRuntime "me.djtheredstoner:DevAuth-neoforge:${rootProject.devauth_version}"
    }

    neoForge {
        runs {
            client {
                ideName = "${project.project_name} Client"
                client()
            }

            server {
                ideName = "${project.project_name} Server"
                server()
            }

            configureEach {
                systemProperty 'forge.logging.markers', 'REGISTRIES'
                logLevel = org.slf4j.event.Level.INFO
            }
        }
    }

    modrinth {
        // Main properties
        token = System.getenv("MODRINTH_TOKEN")
        projectId = rootProject.mod_id
        gameVersions = outlet.mcVersions()
        loaders = ["neoforge"]


        // Version stuff
        def customVersionName = System.getenv("VERSION_NAME")
        if (customVersionName != null) versionName = customVersionName

        versionNumber = "${project.version}"

        def isPreRelease = System.getenv("VERSION_IS_PRERELEASE")
        versionType = Boolean.parseBoolean(isPreRelease) ? "beta" : "release"

        if (project.mod_version.contains("beta")) versionType = "beta"
        if (project.mod_version.contains("alpha")) versionType = "alpha"


        // Files
        uploadFile = jar.archiveFile
        additionalFiles = [sourcesJar.archiveFile]


        // Project info
        syncBodyFrom = rootProject.file("README.md").text
        def changelogEnv = System.getenv("VERSION_CHANGELOG")
        if (changelogEnv != null) changelog = changelogEnv
    }
}
