plugins {
    id 'multiloader-base'
    id 'io.papermc.paperweight.userdev'
    id 'com.gradleup.shadow' apply false
    id 'xyz.jpenilla.run-paper' apply false
    id 'xyz.jpenilla.resource-factory-paper-convention' apply false
    id 'io.github.dexman545.outlet' apply false
}

allprojects {
    apply plugin: "multiloader-base"
    apply plugin: "io.papermc.paperweight.userdev"
    apply plugin: "xyz.jpenilla.resource-factory-paper-convention"
    apply plugin: "io.github.dexman545.outlet"

    outlet {
        maintainPropertiesFile = false
        mcVersionRange = "*"
    }

    paperPluginYaml {
        name = rootProject.mod_name.replace(' ', '_')
        description = rootProject.mod_description
        website = rootProject.mod_website
        apiVersion = outlet.mcVersions().first()
    }

    repositories {
        maven {
            name = 'PaperMC'
            url = 'https://repo.papermc.io/repository/maven-public/'
            content {
                includeGroup "io.papermc.paper"
            }
        }
    }

    configurations {
        include
    }

    dependencies {
        compileOnly project(":common")
        commonJava project(path: ":common", configuration: "commonJava")
        commonResources project(path: ":common", configuration: "commonResources")

        paperweight.paperDevBundle project.paper_version
    }
}

subprojects {
    apply plugin: "multiloader-base"
    apply plugin: "io.papermc.paperweight.userdev"
    apply plugin: "com.gradleup.shadow"
    apply plugin: "xyz.jpenilla.run-paper"
    apply plugin: "io.github.dexman545.outlet"

    outlet {
        mcVersionRange = project.supported_minecraft_versions
    }

    dependencies {
        compileOnly project(":loader:paper")
        commonJava project(path: ":loader:paper", configuration: "commonJava")
        commonResources project(path: ":loader:paper", configuration: "commonResources")
    }
    tasks.build.dependsOn(shadowJar)

    tasks.named("shadowJar") {
        configurations = [project.configurations.include]
        from(rootProject.file("LICENSE")) {
            rename { "${it}" }
        }
    }

    tasks.runServer {
        minecraftVersion project.minecraft_version
    }
}
