plugins {
    id 'multiloader-base'
    id 'io.papermc.paperweight.userdev'
    id 'com.gradleup.shadow' apply false
    id 'xyz.jpenilla.run-paper' apply false
    id 'xyz.jpenilla.resource-factory-paper-convention' apply false
    id 'io.github.dexman545.outlet' apply false
    id 'com.modrinth.minotaur' apply false
}

allprojects {
    apply plugin: "multiloader-base"
    apply plugin: "io.papermc.paperweight.userdev"
    apply plugin: "xyz.jpenilla.resource-factory-paper-convention"
    apply plugin: "io.github.dexman545.outlet"

    outlet {
        maintainPropertiesFile = false
        mcVersionRange = project.supported_minecraft_versions
    }

    paperPluginYaml {
        name = rootProject.mod_name
        description = rootProject.mod_description
        website = rootProject.mod_website
        apiVersion = outlet.mcVersions().first()
    }

    repositories {
        maven {
            name = 'PaperMC'
            url = 'https://repo.papermc.io/repository/maven-public/'
            content {
                includeGroup "io.papermc.paper"
            }
        }
    }

    dependencies {
        compileOnly project(":common")
        commonJava project(path: ":common", configuration: "commonJava")
        commonResources project(path: ":common", configuration: "commonResources")

        paperweight.paperDevBundle project.paper_version
    }
}

subprojects {
    apply plugin: "multiloader-base"
    apply plugin: "io.papermc.paperweight.userdev"
    apply plugin: "com.gradleup.shadow"
    apply plugin: "xyz.jpenilla.run-paper"
    apply plugin: "io.github.dexman545.outlet"
    apply plugin: "com.modrinth.minotaur"

    tasks.modrinth.dependsOn(tasks.modrinthSyncBody)

    dependencies {
        compileOnly project(":loader:paper")
        commonJava project(path: ":loader:paper", configuration: "commonJava")
        commonResources project(path: ":loader:paper", configuration: "commonResources")
    }
    tasks.build.dependsOn(shadowJar)

    tasks.runServer {
        minecraftVersion project.minecraft_version
    }

    modrinth {
        // Main properties
        token = System.getenv("MODRINTH_TOKEN")
        projectId = rootProject.mod_id
        gameVersions = outlet.mcVersions()
        loaders = ["paper"]


        // Version stuff
        def customVersionName = System.getenv("VERSION_NAME")
        if (customVersionName != null) versionName = customVersionName

        versionNumber = "${project.version}"

        def isPreRelease = System.getenv("VERSION_IS_PRERELEASE")
        versionType = Boolean.parseBoolean(isPreRelease) ? "beta" : "release"

        if (project.mod_version.contains("beta")) versionType = "beta"
        if (project.mod_version.contains("alpha")) versionType = "alpha"


        // Files
        uploadFile = shadowJar
        additionalFiles = [sourcesJar.archiveFile]


        // Project info
        syncBodyFrom = rootProject.file("README.md").text
        def changelogEnv = System.getenv("VERSION_CHANGELOG")
        if (changelogEnv != null) changelog = changelogEnv
    }
}
