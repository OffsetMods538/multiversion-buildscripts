plugins {
    id 'java-library'
}

base.archivesName = "${rootProject.mod_id}-${project.project_name}"
version = project.project_name == project.name ? "${rootProject.versionPrefix}+${project.project_name}" : "${rootProject.versionPrefix}+${project.getParent().project_name}+${project.minecraft_version}"

configurations {
    commonJava {
        canBeResolved = true
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = true
        canBeConsumed = true
    }
}

java {
    withSourcesJar()
    withJavadocJar()

    toolchain.languageVersion = JavaLanguageVersion.of(21)
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}


tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

tasks.named("javadoc", Javadoc) {
    dependsOn configurations.commonJava
    source configurations.commonJava
    source = source.asFileTree.matching {
        include '**/api/**'
    }

    options.addFileOption('-add-stylesheet', rootProject.file("javadoc-stylesheet.css"))
    options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.named("sourcesJar", Jar) {
    dependsOn configurations.commonJava
    from configurations.commonJava
    dependsOn configurations.commonResources
    from configurations.commonResources

    from(rootProject.file("LICENSE")) {
        rename { "${it}" }
    }
}

jar {
    from(rootProject.file("LICENSE")) {
        rename { "${it}" }
    }
    manifest {
        attributes("Fabric-Loom-Remap": project.name == "common" ? "false" : "true")
    }
}

tasks.named("compileJava", JavaCompile) {
    dependsOn configurations.commonJava
    source configurations.commonJava
}

processResources {
    dependsOn configurations.commonResources
    from configurations.commonResources
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
}
