plugins {
    id 'java-library'
    id 'maven-publish'
}

base.archivesName = "monkeylib538-${project.project_name}"
version = project.project_name == project.name ? "${rootProject.versionPrefix}+${project.project_name}" : "${rootProject.versionPrefix}+${project.getParent().project_name}+${project.minecraft_version}"

configurations {
    commonJava {
        canBeResolved = true
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = true
        canBeConsumed = true
    }
}

java {
    withSourcesJar()
    withJavadocJar()

    toolchain.languageVersion = JavaLanguageVersion.of(21)
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}


tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

tasks.named("javadoc", Javadoc) {
    dependsOn configurations.commonJava
    source configurations.commonJava

    options.addFileOption('-add-stylesheet', rootProject.file("javadoc-stylesheet.css"))
}

repositories {
    mavenCentral()
    mavenLocal()
}

sourcesJar {
    dependsOn configurations.commonJava
    from configurations.commonJava
    dependsOn configurations.commonResources
    from configurations.commonResources

    from(rootProject.file("LICENSE")) {
        rename { "${it}" }
    }
}

jar {
    from(rootProject.file("LICENSE")) {
        rename { "${it}" }
    }
}

tasks.named("compileJava", JavaCompile) {
    dependsOn configurations.commonJava
    source configurations.commonJava
}

processResources {
    dependsOn configurations.commonResources
    from configurations.commonResources
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
}

publishing {
    repositories {
        maven {
            name = "OffsetMonkey538"
            url = "https://maven.offsetmonkey538.top/releases"
            credentials {
                username = providers.gradleProperty("OffsetMonkey538Username").getOrElse(System.getenv("MAVEN_USERNAME"))
                password = providers.gradleProperty("OffsetMonkey538Password").getOrElse(System.getenv("MAVEN_PASSWORD"))
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        register("maven", MavenPublication) {
            artifactId base.archivesName.get()
            groupId "top.offsetmonkey538.monkeylib538"
            from components.java
        }
    }
}
